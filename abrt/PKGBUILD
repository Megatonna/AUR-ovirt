# Contributor: Andrii Sokolenko
# Based by Fedora abrt.spec
pkgname=abrt-git
pkgver=2.10.2.git3ab10d
pkgrel=1
pkgdesc="Automatic bug detection and reporting tool"
arch=('i686' 'x86_64')
url="https://github.com/abrt/abrt"
license=('GPLv2')
groups=()
makedepends=('git' \
             'gtk3' \
             'dbus' \
             'glib2' \
             'libnotify' \
             'desktop-file-utils' \
             'python2' \
             'python' \
             'gettext' \
             'libxml2' \
             'intltool' \
             'libtool' \
             'nss' \
             'asciidoc' \
             'xmlto' \
             'libreport' \
             'satyr' \
             'python2-systemd' \
             'python-systemd' \
             'augeas' \
             'python-argh' \
             'python2-argh'
             'python-humanize' \
             'python2-humanize' \
             'python-argcomplete' \
             'python2-argcomplete' \
             'json-c' \
             'hawkey' \
             'python-nose' \
             'python-sphinx' \
             'polkit' \
             'dbus-glib')
depends=('libreport' 'satyr' 'shadow' 'python-augeas' 'python-dbus' 'python-dmidecode' 'cpio' 
          'gdb' 'elfutils' 'xz' 'tar' 'curl' 'systemd' 'kexec-tools' 'python2-argh' 'python-argh'
          'python-humanize' 'python2-humanize' 'python-argcomplete' 'python2-argcomplete' 'python2-systemd'
          'python-dbus' 'python2-dbus' 'dbus-glib')
provides=('abrt' 'abrt-addon-vmcore' 'abrt-addon-ccpp' 'abrt-addon-python')
conflicts=()
replaces=()
backup=()
options=()
install=$pkgname.install
source=('git+https://github.com/abrt/abrt.git')
sha256sums=('SKIP')

_systemddir=/usr/lib/systemd
prepare () {
  grep -Rle "#\!/usr/bin/python" * | xargs sed -ie 's/#!\/usr\/bin\/\bpython\b/\#!\/usr\/bin\/python2/g'
  grep -Rle "/usr/bin/env python" * | xargs sed -ie 's/\/usr\/bin\/env \bpython\b/\/usr\/bin\/env python2/g'
  grep -Rle "\[python -B" * | xargs sed -ie 's/\[python -B/\[python2 -B/g'

}
check() {
    cd ${srcdir}/${pkgname%%-git}
    export PYTHON=$(which python2)
    make check
}

build() {
  cd ${srcdir}/${pkgname%%-git}
  export PYTHON=$(which python2)
  sed -ie 's/python-config/python2-config/g' configure.ac
  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/lib/abrt --localstatedir=/var --sysconfdir=/etc \
    --enable-native-unwinder \
    --with-python3 \
    --without-atomic \
    --without-selinux \
    --without-bodhi \
    --without-rpm \
    --enable-dump-time-unwind \
    --disable-silent-rules
  make
}

package() {
  cd ${srcdir}/${pkgname%%-git}
  make DESTDIR="$pkgdir" LIBDIR="/usr/lib" BINDIR="/usr/bin" install
#  mkdir -p ${RPM_BUILD_ROOT}/%{_initrddir}
  install -dDm 0755 $pkgdir/var/cache/abrt-di
  install -dDm 0755 $pkgdir/run/abrt
  install -dDm 0755 $pkgdir/var/spool/abrt
  install -dDm 0755 $pkgdir/var/spool/abrt-upload
  install -Dpm0644 src/cli-ng/abrt.bash_completion $pkgdir/usr/share/bash-completion/completions/abrt
  rm -rf $pkgdir/var/run
}

# vim:set ts=2 sw=2 et:
